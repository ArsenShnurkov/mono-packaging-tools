letterenglishlarge = "A"|"B"|"C"|"D"|"E"|"F"|"G"|"H"|"I"|"J"|"K"|"L"|"M"|"N"|"O"|"P"|"Q"|"R"|"S"|"T"|"U"|"V"|"W"|"X"|"Y"|"Z" ;

letterenglishsmall = "a"|"b"|"c"|"d"|"e"|"f"|"g"|"h"|"i"|"j"|"k"|"l"|"m"|"n"|"o"|"p"|"q"|"r"|"s"|"t"|"u"|"v"|"w"|"x"|"y"|"z" ;

letterrussianlarge = "А"|"Б"|"В"|"Г"|"Д"|"Е"|"Ё"|"Ж"|"З"|"И"|"Й"|"К"|"Л"|"М"|"Н"|"О"|"П"|"Р"|"С"|"Т"|"У"|"Ф"|"Х"|"Ц"|"Ч"|"Ш"|"Щ"|"Ъ"|"Ы"|"Ь"|"Э"|"Ю"|"Я" ;

letterrussiansmall = "а"|"б"|"в"|"г"|"д"|"е"|"ё"|"ж"|"з"|"и"|"й"|"к"|"л"|"м"|"н"|"о"|"п"|"р"|"с"|"т"|"у"|"ф"|"х"|"ц"|"ч"|"ш"|"щ"|"ъ"|"ы"|"ь"|"э"|"ю"|"я" ;

letterenglish = letterenglishlarge | letterenglishsmall ;

letterrussian = letterrussianlarge | letterrussiansmall ;

letter = letterenglish | letterrussian;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

minus =  "-" ;

dquote = '"' ;

backslash = "\\" ;

lbracket = "[";

rbracket = "]";

space = " " | "\t" ;

specialsequence = backslash, dquote | backslash, backslash | backslash, "n" | backslash, "t" | backslash, "b" ;

linecontinuation = backslash, line_separator ;

line_separator = "\n\r" | "\n" | "\r"  ;

raw_symbol_inside_quotes = ? Terminals.AnyChar ? - ( dquote | specialsequence | linecontinuation | line_separator );

non_eol = ? Terminals.AnyChar ? - line_separator ;

startofcomment = "#" | ";" ;

comment = startofcomment , { non_eol } ;

(* at least one SPACE, comments are threated like spaces *)
spaces = ( space | comment ) , { ( space | comment ) } ; 

optional_spaces = [ spaces ] ;

(* at least one EOL *)
line_separators = line_separator, { optional_spaces , line_separator }; 

line_break = { spaces }, line_separators;

(* at least one separator *)
separators = ( spaces | line_break ) , { spaces | line_break } ;

optional_separators = [ separators ] ;

ospace = optional_separators | optional_spaces ;


variablename = letterenglish, { { ( letterenglish | digit | minus | "." ) } , ( letterenglish | digit ) } ;

variable_assignment_long = variablename, ospace , "=", ospace , variablevalue ;

special_include_syntax = "include.path" ,  { separators } ,  "=" ,  { separators } ,  filepath ;

not_a_section_start = non_eol - lbrace ;

malformed_line =  not_a_section_start, { not_a_section_start } ;

bodyentry = variablename | variable_assignment_long | special_include_syntax 
	| malformed_line
	;



variablevalue = quotedstringwithcontinuations | unquoted_string_in_the_wild ;

filepath = variablevalue;

quotedstring = dquote, {raw_symbol_inside_quotes | specialsequence} , dquote ;

quotedstringwithcontinuations = dquote , { raw_symbol_inside_quotes | specialsequence | linecontinuation } , dquote ;

unquoted_string_in_the_wild_unit = ? Terminals.AnyChar ? - ( line_separator | linecontinuation | comment ) ;

unquoted_string_in_the_wild = unquoted_string_in_the_wild_unit , { unquoted_string_in_the_wild_unit } ,
    { { linecontinuation } ,
    unquoted_string_in_the_wild_unit , { unquoted_string_in_the_wild_unit } } ;


subsectiontitleunit = raw_symbol_inside_quotes | specialsequence ;

subsectiontitle = subsectiontitleunit, { subsectiontitleunit } ;

sectiontitleunit = letter | minus | "." ;

sectiontitle = sectiontitleunit, { sectiontitleunit } ;

subsectionheader = lbracket , ospaces , sectiontitle , ospaces , dquote , subsectiontitle , dquote , ospaces , rbracket ;

sectionheader = lbracket, ospaces , sectiontitle, ospaces , rbracket ;


subsectionbody = bodyentry , { ospace , bodyentry } ;

sectionbody = { ospace , bodyentry } , { ospace , subsection } ;

subsection = subsectionheader, { ospace , subsectionbody } ;

section_with_head_separators = ospace;
section_with_head = sectionheader, { section_with_head_separators , sectionbody };
headless_section_separators = ospace;
headless_section = subsection, { headless_section_separators , subsection } ;
section = section_with_head | headless_section ;

intersection_separators = ospace ;
sectionlist = section , { intersection_separators , section } ;

leading_separators = ospace ;
finishing_separators = ospace ;
file_content = leading_separators , { sectionlist } , finishing_separators ;

